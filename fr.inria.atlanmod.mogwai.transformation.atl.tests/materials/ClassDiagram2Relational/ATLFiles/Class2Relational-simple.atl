module Class2Relational;
create OUT : RelationalMM from IN : ClassMM;

-- inheritance not supported yet

-- issue: choose an object-id Type (Integer, String?).
-- We choose Integer here, assuming this type is defined in the source model.

-- global variable
-- context 

helper def: objectIdType : Relational!Type =
	ClassMM!DataType.allInstances()->select(e | e.name = 'Integer')->first();

rule Class2Table {
	from
		c : ClassMM!Class
	to
		out : RelationalMM!Table (
			name <- c.name,
			-- Columns are generated from Attributes in another rule not explicitly called here !
			col <- Sequence {key}->union(c.attr->select(e | not e.multiValued)),
			key <- Set {key}
		),
		key : RelationalMM!Column (
			name <- 'objectId',
			type <- thisModule.objectIdType
		)
}

rule DataType2Type {
	from
		dt : ClassMM!DataType
	to
		out : RelationalMM!Type (
			name <- dt.name
		)
}

rule DataTypeAttribute2Column {
	from
		a : ClassMM!Attribute (
			a.type.oclIsKindOf(ClassMM!DataType) and not a.multiValued
		)
	to
		out : RelationalMM!Column (
			name <- a.name,
			type <- a.type
		)
}
